pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }
}

rootProject.name = 's3-connector'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from("ai.pipestream:pipestream-bom-catalog:0.7.21-SNAPSHOT")
        }
    }

    repositories {
        // Prefer Maven Central but allow locally published releases for development
        mavenLocal()

        mavenCentral()

        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // GitHub Packages for ai.pipestream catalog and dependencies
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                url = uri("https://maven.pkg.github.com/ai-pipestream/bom")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                content {
                    includeGroupByRegex "ai\\.pipestream(\\..*)?"
                }
            }
        }
    }
}
